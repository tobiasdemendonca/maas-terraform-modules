provider "maas" {
  api_key = var.maas_key
  api_url = var.maas_url
  skip_api_checks = var.skip_api_checks
}

locals {
  pre_image_sync_config_keys = toset([
    "enable_http_proxy",
    "http_proxy",
    "https_proxy",
    "boot_images_no_proxy",
  ])

  pre_image_sync_config  = { for k, v in var.maas_config : k => v if contains(local.pre_image_sync_config_keys, k) }
  post_image_sync_config = { for k, v in var.maas_config : k => v if !contains(local.pre_image_sync_config_keys, k) }
}

# 1
# Set boot source
resource "maas_boot_source" "image_server" {
  url              = var.image_server_url
  keyring_filename = "/snap/maas/current/usr/share/keyrings/ubuntu-cloudimage-keyring.gpg"
}

# 2
# Set boot source selections
resource "maas_boot_source_selection" "images" {
  for_each = var.boot_selections

  boot_source = maas_boot_source.image_server.id
  os          = "ubuntu"
  release     = each.key
  arches      = each.value.arches
  subarches   = each.value.subarches

  depends_on = [maas_configuration.pre_image_sync_config]
}

# 3a
# Set MAAS config options that must be applied before boot source selections
# (e.g. proxy settings that affect image downloads)
resource "maas_configuration" "pre_image_sync_config" {
  for_each = local.pre_image_sync_config

  key   = each.key
  value = each.value
}

# 3b
# Set MAAS config options that must be applied after boot source selections
# (e.g. commissioning_distro_series, default_distro_series)
resource "maas_configuration" "post_image_sync_config" {
  for_each = local.post_image_sync_config

  key   = each.key
  value = each.value

  depends_on = [maas_boot_source_selection.images]
}

# 4
# Set MAAS package repositories
resource "maas_package_repository" "package_repositories" {
  for_each = var.package_repositories

  name = each.key
  url  = each.value.url

  arches              = each.value.arches
  components          = each.value.components
  disable_sources     = each.value.disable_sources
  disabled_components = each.value.disabled_components
  disabled_pockets    = each.value.disabled_pockets
  distributions       = each.value.distributions
  enabled             = each.value.enabled
  key                 = each.value.key
}

# 5
# Setup tags
resource "maas_tag" "tags" {
  for_each = var.tags

  name        = each.key
  comment     = each.value.comment
  kernel_opts = each.value.kernel_opts
  definition  = each.value.definition
}

# 6
# Setup domains
resource "maas_dns_domain" "domains" {
  for_each = var.domains

  name          = each.key
  authoritative = each.value.authoritative
  is_default    = each.value.is_default
  ttl           = each.value.ttl
}

# 7
# Setup DNS domain records
resource "maas_dns_record" "test_txt" {
  for_each = { for item in(flatten([
    for domain, records in var.domain_records : [
      for record in records : merge(record, { "domain" : domain })
    ]
    ])) : "${item.domain}_${item.name}" => item
  }
  type   = each.value.type
  data   = each.value.data
  name   = each.value.name
  domain = maas_dns_domain.domains[each.value.domain].id
}

# 8
# Setup commissioning scripts
resource "maas_node_script" "node_scripts" {
  for_each = var.node_scripts

  script = base64encode(file("${var.node_scripts_location}/${each.value}"))
}

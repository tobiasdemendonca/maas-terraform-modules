include "root" {
  path = find_in_parent_folders("root.hcl")
}

terraform {
  // NOTE: Take note that this source here uses
  // a Git URL instead of a local path.
  //
  // This is because units and stacks are generated
  // as shallow directories when consumed.
  //
  // Assume that a user consuming this unit will exclusively have access
  // to the directory this file is in, and nothing else in this repository.
  source = "git::https://github.com/canonical/maas-terraform-modules.git//modules/maas-config?ref=${values.version}"
}

dependency "maas_deploy" {
  config_path = try(values.maas_deploy_path, null)

  mock_outputs_merge_strategy_with_state = "shallow"

  mock_outputs = {
    maas = {
      api_url          = "http://mock-maas"
      api_key          = "mock:mock:mock"
      tls_ca_cert_path = null
      skip_api_checks  = true # Enables `stack run plan` only when mocks are used; real runs get no `skip_api_checks` key from maas-deploy and fall back to the variable's `optional(bool, false)` default. Relies on mock_outputs_merge_strategy_with_state = "shallow"
    }
  }
}

dependencies {
  paths = try(values.dependencies, [])
}

locals {

  optional_inputs = {
    image_server_url      = try(values.image_server_url, null)
    boot_selections       = try(values.boot_selections, null)
    maas_config           = try(values.maas_config, null)
    package_repositories  = try(values.package_repositories, null)
    tags                  = try(values.tags, null)
    domains               = try(values.domains, null)
    domain_records        = try(values.domain_records, null)
    node_scripts          = try(values.node_scripts, null)
    node_scripts_location = try(values.node_scripts_location, null)
  }
}

inputs = merge({
  // Optional inputs (only passed if defined in the stacks config)
  for k, v in local.optional_inputs :
  k => v
  if v != null
  },
  {
    // Dependent variables
    maas = coalesce(try(values.maas, null), try(dependency.maas_deploy.outputs.maas, null))

    // Required variables
    // (none)
})

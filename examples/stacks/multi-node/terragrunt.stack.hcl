locals {
  // These are nullable values
  maas_url        = trimspace(get_env("MAAS_URL", ""))
  virtual_ip      = trimspace(get_env("VIRTUAL_IP", ""))
  ssl_cert_path   = trimspace(get_env("SSL_CERT_PATH", ""))
  ssl_key_path    = trimspace(get_env("SSL_KEY_PATH", ""))
  ssl_cacert_path = trimspace(get_env("SSL_CACERT_PATH", ""))
}

unit "juju_bootstrap" {
  // This unit comes from the official Juju Terraform controller catalog.
  // You'll typically want to pin this to a particular release of that repo.
  source = "git::https://github.com/juju/terraform-juju-controller.git//terragrunt/units/juju_bootstrap?ref=v0.0.1-rc6"

  path = "juju-bootstrap"

  values = {
    // The Terraform Registry version of the juju/juju-controller/juju module to use.
    version = "0.0.1-rc6"

    // Required variables
    // The name of the Juju controller to bootstrap.
    name = "maas-controller"
    // Number of controller units to deploy. Keep at 1 for a single controller.
    controller_num_units = 1

    // The cloud to bootstrap the Juju controller onto. This example targets LXD.
    cloud = {
      auth_types = ["certificate"]
      name       = "maascloud"
      type       = "lxd"
      // The API endpoint URL that Juju should use to communicate with LXD.
      endpoint = get_env("LXD_ADDRESS")
      region = {
        name     = "default"
        endpoint = get_env("LXD_ADDRESS")
      }
    }

    // The credentials Juju uses to authenticate to the cloud.
    cloud_credential = {
      auth_type = "interactive"
      name      = "maascloud-token"
      attributes = {
        // The LXD trust token that Juju should use to authenticate to LXD.
        trust-token = get_env("LXD_TRUST_TOKEN")
      }
    }

    // Optional variables
    // Controller model config. For LXD, set the project for the controller resources.
    // controller_model_config = {
    //   project = "charmed-maas"
    // }
    // Map of model defaults to set for all models (e.g., http-proxy, https-proxy, no-proxy, apt-http-proxy, etc.)
    // model_default = {}
    // Example:
    // model_default = {
    //   http-proxy       = "http://squid:3128"
    //   https-proxy      = "http://squid:3128"
    //   no-proxy         = "localhost,127.0.0.1"
    //   apt-http-proxy   = "http://squid:3128"
    //   apt-https-proxy  = "http://squid:3128"
    //   snap-http-proxy  = "http://squid:3128"
    //   snap-https-proxy = "http://squid:3128"
    // }
    // Additional flags for destroying the controller
    // destroy_flags = {
    //   destroy_all_models = true
    //   destroy_storage    = true
    // }
    // Constraints for the controller machine, map of strings
    // bootstrap_constraints = {
    //    "cores" = "1"
    //    "mem"   = "2G"
    // }
  }
}

unit "maas_deploy" {
  // You'll typically want to pin this to a particular version of your catalog repo.
  // e.g.
  // source = "git::git@github.com:canonical/maas-terraform-modules.git//units/maas-deploy?ref=v0.1.0"
  source = "../../../units/maas-deploy"

  path = "maas-deploy"

  values = {
    // This version here is used as the version passed down to the unit
    // to use when fetching the OpenTofu/Terraform module.
    version = "main"

    // Dependencies
    juju_bootstrap_path = "../juju-bootstrap"

    // Required variables
    // (none)

    // Optional variables
    // Uncomment and complete to customize. Defaults are shown where defined in variables.tf.
    // The LXD project in which to create the VMs for Juju
    lxd_project = get_env("LXD_PROJECT_MAAS_MACHINES", "default")
    // Map of additional model configuration parameters (e.g., http-proxy, https-proxy, no-proxy, etc.)
    // model_config = ...
    // Example:
    // model_config = {
    //   juju-http-proxy  = "http://10.21.2.1:3128"
    //   juju-https-proxy = "http://10.21.2.1:3128"
    //   juju-no-proxy    = "10.0.0.1/24,10.21.2.0/24,localhost,127.0.0.1"
    // }
    // Path to the SSH key to add to the MAAS Juju model
    // path_to_ssh_key = ...
    // Example:
    // path_to_ssh_key = "/home/ubuntu/.ssh/id_ed25519.pub"

    // -- Machines and constraints
    // Map of Juju model constraints to apply to the MAAS model (e.g. to pin the architecture).
    // model_constraints = {
    //   arch = "arm64"
    // }
    // Use the following constraints for the machines. Increase cores and mem for larger MAAS installations. We recommend using virtual machines.
    // If you are curious you can change the constraints to use containers or physical
    // hosts but this is untested
    maas_constraints = "cores=4 mem=4G virt-type=virtual-machine root-disk=20G root-disk-source=default"
    // Constraints for the PostgreSQL virtual machines
    postgres_constraints = "cores=2 mem=4G virt-type=virtual-machine"
    // Constraints for the HAProxy Machines
    haproxy_constraints = "cores=1 mem=1G"
    // Constraints for the S3 Integrator machine
    s3_constraints = "cores=1 mem=1G"
    // List of target zones for deploying MAAS and PostgreSQL machines. If provided, machines
    // are distributed across these zones in round-robin fashion (for example, with 3 zones and
    // 3 machines, each gets a different zone; with 2 zones and 3 machines, the pattern is
    // zone1, zone2, zone1).
    // zone_list            = ...
    // Set this to true to run PostgreSQL in high availability (HA), which will create three PostgreSQL units
    enable_postgres_ha = true
    // Set this to true to run MAAS in high availability (HA), which will create three maas-region controller units
    enable_maas_ha = true
    // Set this to true to run MAAS with HAProxy, which will deploy HAProxy
    enable_haproxy = true
    // The Ubuntu operating system version to install on the MAAS region controller machines
    maas_ubuntu_version = "24.04"
    // The Ubuntu operating system version to install on the PostgreSQL machines
    postgres_ubuntu_version = "24.04"
    // The Ubuntu operating system version to install on the HAProxy machines
    haproxy_ubuntu_version = "24.04"

    // -- Workload: PostgreSQL
    // Operator channel for PostgreSQL deployment
    charm_postgresql_channel = "16/stable"
    // Operator channel revision for PostgreSQL deployment
    // charm_postgresql_revision  = ...
    // Operator configuration for PostgreSQL deployment
    charm_postgresql_config = {
      // Maximum number of concurrent connections to allow to the database server
      experimental_max_connections = 400
      // Enable btree_gin PostgreSQL plugin since it is required by Temporal, a MAAS >=3.5 key component
      plugin_btree_gin_enable = true
    }

    // --- Workload: HAProxy ---
    // Operator channel for HAProxy deployment
    charm_haproxy_channel = "2.8/edge"
    // Operator channel revision for HAProxy deployment
    // charm_haproxy_revision  = ...
    // Operator configuration for HAProxy deployment
    // charm_haproxy_config = {}

    // --- Workload: Keepalived ---
    // Operator channel for Keepalived deployment
    charm_keepalived_channel = "latest/edge"
    // Operator channel revision for Keepalived deployment
    // charm_keepalived_revision  = ...
    // Operator configuration for Keepalived deployment
    charm_keepalived_config = {
      port = 80
    }

    // -- Workload: MAAS
    // Operator channel for MAAS Region Controller deployment
    charm_maas_region_channel = "3.7/candidate"
    // Operator channel revision for MAAS Region Controller deployment
    // charm_maas_region_revision = ...
    // Operator configuration for MAAS Region Controller deployment
    charm_maas_region_config = {
      enable_rack_mode = true
    }

    // -- MAAS Admin configuration
    // The MAAS admin username
    admin_username = "admin"
    // The MAAS admin password
    admin_password = get_env("MAAS_ADMIN_PASSWORD")
    // The MAAS admin email
    admin_email = "admin@maas.io"
    // The MAAS admin SSH key source. Valid sources include 'lp' for Launchpad and 'gh' for GitHub. E.g. 'lp:my_launchpad_username'.
    admin_ssh_import = get_env("ADMIN_SSH_IMPORT", "")

    // --- MAAS API Configuration ---
    // The MAAS URL to use for the MAAS API. If not given, will default to one derived from the `virtual_ip`, or the HAProxy/MAAS Unit IPs
    maas_url = local.maas_url != "" ? local.maas_url : null
    // The optional Virtual IP to use for HA MAAS. If given, will configure the Keepalived subordinate charm.
    virtual_ip = local.virtual_ip != "" ? local.virtual_ip : null
    // SSL Certificate path, Required for MAAS TLS mode operations
    ssl_cert_path = local.ssl_cert_path != "" ? local.ssl_cert_path : null
    // SSL Key path, Required for MAAS TLS mode operations
    ssl_key_path = local.ssl_key_path != "" ? local.ssl_key_path : null
    // SSL CACert path, optionally used for MAAS TLS mode operations if the ssl_certificate is self signed
    ssl_cacert_path = local.ssl_cacert_path != "" ? local.ssl_cacert_path : null

    // -- External integrations (backup/s3)
    // Whether to enable backup for MAAS and PostgreSQL
    enable_backup = false
    // Operator channel for S3 Integrator deployment
    charm_s3_integrator_channel = "1/stable"
    // Operator channel revision for S3 Integrator deployment
    // charm_s3_integrator_revision = ...
    // Operator configuration for both S3 Integrator deployments. Configuration for `bucket`, `path`, and `tls-ca-chain` is skipped even if set, since it is handled by different Terraform variables.
    charm_s3_integrator_config = {
      // Endpoint the S3 backup exists at
      endpoint = "https://10.10.0.26:9000/"
      // The AWS region the S3 bucket is in. Leave empty for Minio
      region = ""
      // The S3 protocol specific bucket path lookup type. Leave `path` for Minio
      s3-uri-style = "path"
    }
    // The file path of the S3 CA chain, used for HTTPS validation.
    s3_ca_chain_file_path = get_env("S3_CA_CHAIN_FILE_PATH", "")
    // Access key used to access the S3 backup bucket
    s3_access_key = get_env("S3_ACCESS_KEY", "")
    // Secret key used to access the S3 backup bucket
    s3_secret_key = get_env("S3_SECRET_KEY", "")
    // Bucket name to store PostgreSQL backups in
    s3_bucket_postgresql = "postgresql-backup"
    // Path in the S3 bucket to store PostgreSQL backups in
    s3_path_postgresql = "postgresql-backups"
    // Bucket name to store MAAS backups in
    s3_bucket_maas = "maas-backup"
    // Path in the S3 bucket to store MAAS backups in
    s3_path_maas = "/maas-backups"
  }
}

unit "maas_config" {
  // You'll typically want to pin this to a particular version of your catalog repo.
  // e.g.
  // source = "git::git@github.com:canonical/maas-terraform-modules.git//units/maas-config?ref=v0.1.0"
  source = "../../../units/maas-config"

  path = "maas-config"

  values = {
    // This version here is used as the version passed down to the unit
    // to use when fetching the OpenTofu/Terraform module.
    version = "main"

    // Dependencies
    maas_deploy_path = "../maas-deploy"

    // Optional variables
    // The URL of the boot source to synchronize OS images from. This needs to be a simple streams server
    image_server_url = "http://images.maas.io/ephemeral-v3/stable/"
    // Configure MAAS to download these images immediately. Each key is the release name and the value is a map of architectures and - optionally - sub-architectures. You must specify all images used in your deployment here, including the automatically synced default commissioning image.
    boot_selections = {
      noble = {
        arches    = ["amd64"]
        subarches = ["generic"]
      }
    }
    // A map of package repositories to supply to MAAS deployed machines, where key is the repository name and value is a map of package repository settings
    package_repositories = {
      foo_bar = {
        url    = "http://foo.bar.com/foobar"
        arches = ["amd64", "arm64"]
      }
    }
    // A map of MAAS configuration settings, where key is the setting name and value is the setting desired value. Note that some settings such as `commissioning_distro_series` and `default_distro_series` require images to be synced before they can be configured. The values for these settings must match images defined in `boot_selections`, including the automatically synced default commissioning image.
    maas_config = {
      "default_osystem" = "ubuntu"
    }
    // A map of tags to create, where key is the tag name and value is a map of tag attributes
    tags = {
      "gpu-node" = {
        comment = "Nodes with GPU hardware"
      },
      "gpgpu-tesla-vi" = {
        // See here for more details on tag management: https://discourse.maas.io/t/maas-ui-automatic-tags-and-tag-management/5565
        comment     = "Example tag for enabling passthrough for Nvidia Tesla V series GPUs on Intel. "
        kernel_opts = "console=tty0 console=ttyS0,115200n8r nomodeset modprobe.blacklist=nouveau,nvidiafb,snd_hda_intel nouveau.blacklist=1 video=vesafb:off,efifb:off intel_iommu=on rd.driver.pre=pci-stub rd.driver.pre=vfio-pci pci-stub.ids=10de:1db4 vfio-pci.ids=10de:1db4 vfio_iommu_type1.allow_unsafe_interrupts=1 vfio-pci.disable_vga=1"
        definition  = "//node[@id=\"cpu:0\"]/capabilities/capability/@id = \"vmx\" and //node[@id=\"display\"]/vendor[contains(.,\"NVIDIA\")] and //node[@id=\"display\"]/description[contains(.,\"3D\")] and //node[@id=\"display\"]/product[contains(.,\"Tesla V100 PCIe 16GB\")]"
      }
    }
    // A map of DNS domains to create, where key is the domain name and value is a map of domain attributes
    domains = {
      "example.maas" = {
        ttl           = 3600
        is_default    = false
        authoritative = true
      }
    }
    // A map of DNS domain records to create, where key is the domain name and value is a set domain records. Each domain record is a map of domain record attributes
    domain_records = {
      "example.maas" = [
        {
          name = "web"
          type = "A/AAAA"
          data = "192.168.1.100"
          ttl  = 300
        }
      ]
    }
    // A set of node scripts to create, where each set item points to the script file path relative to node_scripts_location
    node_scripts = ["testing-script.sh"]
    // The path in disk where node script files are located
    node_scripts_location = "${get_terragrunt_dir()}/../resources"
  }
}
